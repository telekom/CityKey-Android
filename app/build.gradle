apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

apply from: '../config/common.gradle'
apply from: '../config/secrets.gradle'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion = rootProject.compileSdkVersion
    buildToolsVersion = rootProject.buildToolsVersion
    flavorDimensions = ["default"]

    def APP_VERSION = "1.3.16"
    def MAPS_API_KEY = getSecret('MAPS_API_KEY')

    defaultConfig {
        applicationId "com.telekom.citykey"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 110
        versionName APP_VERSION
        multiDexEnabled = true
        manifestPlaceholders = [MAPS_API_KEY: MAPS_API_KEY]
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "APP_VERSION", "\"$APP_VERSION\""
        resourceConfigurations += ['en', 'de']

        buildConfigField "String", "APPCENTER_ID", "\"X.X.X\""

        buildConfigField "String", "FLAVOR", "\"production\""
        buildConfigField "String", "TPNS_HOST_NAME", "\"about:blank\""
        buildConfigField "String", "HOST_NAME", "\"about:blank\""
        buildConfigField "String", "IMAGE_URL", "\"file:///android_asset/\""
        buildConfigField "int", "CITY_ID", "8"
        buildConfigField "String", "SERVICES_VERSION", "\"X.X\""
        buildConfigField "String", "ADJUST_APP_TOKEN", "\"X.X\""
        buildConfigField "String", "MOENGAGE_APP_ID", "\"X.X\""
        buildConfigField "String", "KOMMUNIX_ENV_REQUEST", "\"X.X\""

        // For generating Native Debug Symbols
        ndkVersion = '25.1.8937393'
        ndk {
            debugSymbolLevel = 'full'
        }
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'

        // NOTE: When the `native-debug-symbols.zip` file size gets larger than 300 MB,
        // then need to change this value to 'symbol_table'.
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        debug
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
        }
    }

    packagingOptions {
        jniLibs { useLegacyPackaging = true }
        resources.excludes.add("META-INF/*")
    }
    namespace 'com.telekom.citykey'
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    // NOTE: This dependency is required for resolution of the error regarding `kotlin-stdlib`
    // and the duplicate classes found issue
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    /* =================== AndroidX Section =================== */

    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.annotation:annotation:1.6.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.paging:paging-runtime-ktx:3.2.0"
    implementation "androidx.paging:paging-rxjava2-ktx:3.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.1"
    implementation "androidx.lifecycle:lifecycle-process:2.6.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.fragment:fragment-ktx:1.6.1"
    implementation "androidx.browser:browser:1.5.0"
    implementation "androidx.webkit:webkit:1.7.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.6.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.6.0"
    implementation "androidx.preference:preference-ktx:1.2.0"

    /* =================== End AndroidX Section =================== */

    /* =================== Google Android Section =================== */

    implementation "com.google.android.material:material:1.9.0"
    implementation "com.google.android.gms:play-services-auth:20.6.0"
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation "com.google.android.gms:play-services-location:21.0.1"
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    //Push notifications from Firebase
    implementation 'com.google.firebase:firebase-messaging-ktx:23.2.0'

    /* =================== End Google Android Section =================== */

    /* =================== App Dependencies Section =================== */

    implementation "io.reactivex.rxjava2:rxkotlin:${rxKotlinVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation "io.insert-koin:koin-android:${koinVersion}"

    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    kapt "com.github.bumptech.glide:compiler:${glideVersion}"
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'

    implementation "com.google.zxing:core:3.3.0"
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.douglasjunior:android-simple-tooltip:1.1.0'
    implementation 'com.scottyab:rootbeer-lib:0.1.0'
    implementation 'com.github.iAmMONK:HtmlSpanner:0.5.1'
    implementation "com.airbnb.android:lottie:5.2.0"

    /* =================== End App Dependencies Section =================== */

    /* =================== SDKs Section =================== */

    // Ausweis App
    implementation "com.governikus:ausweisapp:1.26.5"

    // Adjust for tracking
    implementation 'com.adjust.sdk:adjust-android:5.0.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // Moengage SDK
    implementation "com.moengage:moe-android-sdk:13.05.00"
    implementation 'com.moengage:inapp:8.6.0'

    // Microsoft AppCenter
    def appCenterSdkVersion = "4.2.0"
    implementation "com.microsoft.appcenter:appcenter-distribute-play:${appCenterSdkVersion}"

    /* =================== End SDKs Section =================== */

    /* =================== Test Section =================== */

    // Unit Tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.0"
    testImplementation "io.mockk:mockk:1.13.5"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.assertj:assertj-core:3.24.2"


    // UI tests
    androidTestImplementation("androidx.test:runner:1.6.1")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.6.1")

    /* =================== End Test Section =================== */
}

apply from: '../config/quality.gradle'
