/**
 * Copyright (C) 2025 Deutsche Telekom AG
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * In accordance with Sections 4 and 6 of the License, the following exclusions apply:
 *
 *  1. Trademarks & Logos – The names, logos, and trademarks of the Licensor are not covered by this License and may not be used without separate permission.
 *  2. Design Rights – Visual identities, UI/UX designs, and other graphical elements remain the property of their respective owners and are not licensed under the Apache License 2.0.
 *  3: Non-Coded Copyrights – Documentation, images, videos, and other non-software materials require separate authorization for use, modification, or distribution.
 *
 * These elements are not considered part of the licensed Work or Derivative Works unless explicitly agreed otherwise. All elements must be altered, removed, or replaced before use or distribution. All rights to these materials are reserved, and Contributor accepts no liability for any infringing use. By using this repository, you agree to indemnify and hold harmless Contributor against any claims, costs, or damages arising from your use of the excluded elements.
 *
 * SPDX-FileCopyrightText: 2025 Deutsche Telekom AG
 * SPDX-License-Identifier: Apache-2.0 AND LicenseRef-Deutsche-Telekom-Brand
 * License-Filename: LICENSES/Apache-2.0.txt LICENSES/LicenseRef-Deutsche-Telekom-Brand.txt
 */

apply plugin: 'io.gitlab.arturbosch.detekt'

configurations {
    detekt
    ktlintCityKeyAndroid
}

dependencies {
    ktlintCityKeyAndroid 'com.pinterest:ktlint:0.43.1'
}

def variants = android.hasProperty('libraryVariants') ? android.libraryVariants : android.applicationVariants

variants.all { variant ->
    task("check${variant.name.capitalize()}", dependsOn: [
            "lint${variant.name.capitalize()}",
            "detektC${variant.name.capitalize()}",
            "ktlint${variant.name.capitalize()}"
    ]) { group = "Verification" }
}

variants.all { variant ->
    task("detektC${variant.name.capitalize()}", type: JavaExec) {
        group = "Verification"
        main = "io.gitlab.arturbosch.detekt.cli.Main"
        classpath = configurations.detekt

        def input = projectDir
        def output = "${buildDir}/reports/detekt/"
        def outputName = "${variant.name}.xml"
        def report = "xml:$output$outputName"
        def exclude = ".*/build/.*,.*/resources/.*"
        def config = "${project.rootDir}/config/quality/detekt/default-config.yml"

        args(['-i', input, '-r', report, '-c', config, '-ex', exclude, '--fail-fast'])
    }
}

variants.all { variant ->
    task("ktlint${variant.name.capitalize()}", type: JavaExec) {
        group = "Verification"
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlintCityKeyAndroid
        ignoreExitValue = true

        def input = getSourceDirs(variant).collect { "\"$it/**/*.kt\"" }
        def output = "${buildDir}/reports/ktlint/${variant.name}.xml"

        args(['--android', "--reporter=plain?group_by_file", "--reporter=checkstyle,output=${output}"] + input)
    }
}

task ktlintFormatCityKeyAndroid(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlintCityKeyAndroid
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
    ignoreExitValue = true
}


static def getSourceDirs(variant) {
    return variant.sourceSets*.name.collect { "src/$it/java" }
}
